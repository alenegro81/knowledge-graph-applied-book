CALL apoc.periodic.iterate(
    "MATCH (dis:HpoDisease)-[rel:HAS_PHENOTYPIC_FEATURE]->(phe:HpoPhenotype) RETURN rel",
    "SET rel.createdBy = apoc.text.regexGroups(rel.biocuration, 'HPO:(\\w+)\\[')[0][1],
     rel.creationDate = apoc.text.regexGroups(rel.biocuration, '\\[(\\d{4}-\\d{2}-\\d{2})\\]')[0][1],
     rel.aspectName = 
     CASE  
         WHEN rel.aspect = 'P' THEN 'Phenotypic abnormality' 
         WHEN rel.aspect = 'I' THEN 'Inheritance' 
     END, 
     rel.aspectDescription = 
     CASE 
         WHEN rel.aspect = 'P' THEN 'Terms with the P aspect are located in the Phenotypic abnormality subontology' 
         WHEN rel.aspect = 'I' THEN 'Terms with the I aspect are from the Inheritance subontology' 
     END, 
     rel.evidenceName = 
     CASE  
         WHEN rel.evidence = 'IEA' THEN 'Inferred from electronic annotation' 
         WHEN rel.evidence = 'PCS' THEN 'Published clinical study' 
         WHEN rel.evidence = 'TAS' THEN 'Traceable author statement' 
     END, 
     rel.evidenceDescription = 
     CASE 
         WHEN rel.evidence = 'IEA' THEN 'Annotations extracted by parsing the Clinical Features sections of the Online Mendelian Inheritance in Man resource are assigned the evidence code IEA.' 
         WHEN rel.evidence = 'PCS' THEN 'PCS is used for information extracted from articles in the medical literature. Generally, annotations of this type will include the pubmed id of the published study in the DB_Reference field.' 
         WHEN rel.evidence = 'TAS' THEN 'TAS is used for information gleaned from knowledge bases such as OMIM or Orphanet that have derived the information from a published source.' 
     END, 
     rel.url = 
     CASE 
         WHEN rel.source STARTS WITH 'PMID:' THEN 'https://pubmed.ncbi.nlm.nih.gov/' + apoc.text.replace(rel.source, '(.*)PMID:', '') 
         WHEN rel.source STARTS WITH 'OMIM:' THEN 'https://omim.org/entry/' + apoc.text.replace(rel.source, '(.*)OMIM:', '') 
     END",
{batchSize: 1000})
